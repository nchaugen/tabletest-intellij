{
  parserClass="io.github.nchaugen.tabletest.language.parser.TableTestParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="TableTest"
  psiImplClassSuffix="Impl"
  psiPackage="io.github.nchaugen.tabletest.language.psi"
  psiImplPackage="io.github.nchaugen.tabletest.language.psi.impl"

  elementTypeHolderClass="io.github.nchaugen.tabletest.language.psi.TableTestTypes"
  elementTypeClass="io.github.nchaugen.tabletest.language.psi.TableTestElementType"
  tokenTypeClass="io.github.nchaugen.tabletest.language.psi.TableTestTokenType"

  tokens = [
    PIPE='|'
    LEFT_BRACKET='['
    RIGHT_BRACKET=']'
    COMMA=','
    COLON=':'
    DOUBLE_QUOTE='"'
    SINGLE_QUOTE="'"
    NEWLINE='\n'
  ]
}

table ::= header_row row*

header_row ::= header ('|' header)* '\n'
header ::= INPUT_HEADER | OUTPUT_HEADER
row ::= element? ('|' element?)* '\n'

element ::= list | map | string

list ::= '[' (element (',' element)* )? ']'

map ::= '[' map_entry (',' map_entry)* ']' | EMPTY_MAP
map_entry ::= MAP_KEY ':' element

string ::= ('"' STRING_VALUE? '"') | ("'" STRING_VALUE? "'") | STRING_VALUE
